#include "aclocal.m4"

AC_SUBST(YACC)
AC_SUBST(SHELL)
AC_SUBST(ARCH)
AC_SUBST(EXEEXT)
AC_SUBST(INSTALL_DX_EXE)
AC_SUBST(INSTALL_DX_CSH)
AC_SUBST(INSTALL_SCRIPT)
AC_SUBST(FMT_LIBS)
AC_SUBST(LIBS)
AC_SUBST(JBASE)
AC_SUBST(JDKBIN)
AC_SUBST(JAVA_ARCH)
AC_SUBST(WRL_CLASSPATH)


SHELL=sh

INSTALL_SCRIPT='$(INSTALL)'


dnl Process this file with autoconf to produce a configure script.
AC_INIT(include/dx/arch.h)
AM_CONFIG_HEADER(include/dxconfig.h)
AM_INIT_AUTOMAKE(dx, 4.0.6)

AC_DEFINE(DXD_VERSION_STRING,   "04.01.0006")
AC_DEFINE_UNQUOTED(DXD_VERSION, 4)
AC_DEFINE_UNQUOTED(DXD_RELEASE, 1)
AC_DEFINE_UNQUOTED(DXD_MODIFICATION, 6)

DX_ARCHITECTURE

AC_CHECK_PROG(HAVE_CSH, csh, 1, 0)
	
if test "$HAVE_CSH" = "1" ; then
    INSTALL_DX_EXE=
    INSTALL_DX_CSH="dx dx.workerscript"
else
    INSTALL_DX_EXE=dx
    INSTALL_DX_CSH=
fi

AC_PROG_INSTALL

dnl Checks for programs.
AC_PROG_RANLIB
DX_PROG_CXX()
DX_PROG_CC
DX_PROG_CPP
AC_CYGWIN
AC_EXEEXT

DX_FIND_JDK()

AC_LANG_C

DX_CYGWIN_MOUNTS

AC_PATH_X
if test "$x_includes" != "" ; then
    CFLAGS="$CFLAGS -I$x_includes"
    CXXFLAGS="$CXXFLAGS -I$x_includes"
    CPPFLAGS="$CPPFLAGS -I$x_includes"
fi
if test "$x_libraries" != "" ; then
    LIBS="-L$x_libraries $LIBS"
fi

DX_PATH_XM
if test "$xm_includes" != "" ; then
    CFLAGS="$CFLAGS -I$xm_includes"
    CXXFLAGS="$CXXFLAGS -I$xm_includes"
    CPPFLAGS="$CPPFLAGS -I$xm_includes"
fi
if test "$xm_libraries" != "" ; then
    LIBS="-L$xm_libraries $LIBS"
fi

AC_TRY_LINK([
#include <Xm/FormP.h>
],
[
XmFormConstraintPart *p;
p->att[0].type;
],
,
[
    AC_DEFINE_UNQUOTED(OLD_LESSTIF, 1)
    echo "using old version of  LESSTIF"
])

if test "$ARCH" = "solaris" ; then
    OLDLIBS=$LIBS
    LIBS="-L/usr/openwin/lib $LIBS"
    AC_CHECK_LIB(dga, dga_draw_devfd, 
    [
      LIBS="$LIBS -ldga"
      AC_CHECK_LIB(Xmu, XmuAddInitializer, LIBS="$LIBS -lXmu")
    ], LIBS=$OLDLIBS, -lX11)
fi

AC_CHECK_LIB(gen, regex)

dnl A couple libs for cygwin
AC_CHECK_LIB(ICE, IceConnectionNumber)
AC_CHECK_LIB(SM, SmcSetProperties)
AC_CHECK_LIB(REXP, regcomp)
AC_CHECK_LIB(pthread, pthread_getspecific)

DX_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/socket.h sys/file.h sys/ioctl.h time.h sys/timeb.h sys/time.h unistd.h sys/types.h sys/sysconfig.h sys/un.h values.h wait.h sys/wait.h sys/utsname.h signal.h sys/signal.h process.h sys/filio.h CC/osfcn.h CC/libc.h sys/param.h sys/select.h cygwin/socket.h winsock.h netinet/in.h netdb.h arpa/inet.h types.h windows.h)
AC_CHECK_FUNCS(getcwd gethostname gettimeofday mkdir mkfifo mktime putenv re_comp regcmp select socket strcspn strdup strerror strspn strstr strtod strtol strtoul uname popen vfork trunc _popen spawnvp _spawnvp regcomp sysmp sysconf)

AC_TRY_COMPILE(
[],
[extern char *getenv(const char *);],
AC_DEFINE_UNQUOTED(GETENV_ARG, const char *),
AC_DEFINE_UNQUOTED(GETENV_ARG, char *))

AC_TRY_LINK(
[
#include <sys/systemcfg.h>
],
[
int n = _system_configuration.ncpus;
],
[
AC_DEFINE_UNQUOTED(HAVE_SYSCONFIG_NCPUS)
])

AC_EGREP_HEADER(sys_errlist, errno.h, AC_DEFINE(HAVE_SYS_ERRLIST))
AC_EGREP_HEADER(sys_errlist, stdio.h, AC_DEFINE(HAVE_SYS_ERRLIST))


dnl Check yaccer.  Don't use standard AQC_PROG_YACC... We're going
dnl to create an -D as well as the make variable
AC_CHECK_PROG(yakker, bison, "bison", "yacc")
if test "$yakker" = "bison" ; then
    AC_DEFINE(USING_BISON)
    YACC="bison -y"
else
    YACC=yacc
fi

dnl Checks for libraries.
AC_CHECK_LIB(g++, main)
AC_CHECK_LIB(dl, main)
AC_CHECK_LIB(z, compress,
[
    AC_DEFINE(HAVE_LIBZ)
    LIBS="-lz $LIBS"
    FMT_LIBS="$FMT_LIBS libz.a"
])
AC_CHECK_LIB(cdf, CDFlib,
[
    AC_DEFINE(HAVE_LIBCDF)
    LIBS="-lcdf $LIBS"
    FMT_LIBS="$FMT_LIBS libcdf.a"
])
AC_CHECK_LIB(jpeg, jpeg_set_quality,
[
    AC_DEFINE(HAVE_LIBJPEG)
    LIBS="-ljpeg $LIBS"
    FMT_LIBS="$FMT_LIBS libjpeg.a"
])
AC_CHECK_LIB(df, Hopen,
[
    AC_DEFINE(HAVE_LIBDF)
    LIBS="-ldf $LIBS"
    FMT_LIBS="$FMT_LIBS libdf.a"
])
AC_CHECK_LIB(netcdf, free_NC_attr,
[
    AC_DEFINE(HAVE_LIBNETCDF)
    LIBS="-lnetcdf $LIBS"
    FMT_LIBS="$FMT_LIBS libnetcdf.a"
])

AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, inet_addr)

DX_STREAM_O2

AC_LANG_CPLUSPLUS

AC_TRY_LINK([
#include <unistd.h>
], [
char buf[32];
gethostname(buf, 32);
],
AC_DEFINE_UNQUOTED(REQUIRES_GETHOSTNAME_DECLARATION, 0),
AC_DEFINE_UNQUOTED(REQUIRES_GETHOSTNAME_DECLARATION, 1))

AC_TRY_LINK([
#include <strings.h>
], [
char buf[32];
bzero(buf, (size_t)32);
],
AC_DEFINE_UNQUOTED(REQUIRES_BZERO_DECLARATION, 0),
AC_DEFINE_UNQUOTED(REQUIRES_BZERO_DECLARATION, 1))

AC_LANG_C

dnl Checks for header files.
AC_PATH_X
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

DX_HEADER_HAS_SYMBOL(math.h, M_PI)
DX_HEADER_HAS_SYMBOL(math.h, M_SQRT2)
DX_HEADER_HAS_SYMBOL(sys/stat.h, S_ISDIR)
DX_HEADER_HAS_SYMBOL(sys/sysmp.h, sysmp)

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_SETVBUF_REVERSED
AC_TYPE_SIGNAL
AC_FUNC_VFORK
AC_FUNC_VPRINTF

DX_CHECK_TYPE(caddr_t, char **)
DX_CHECK_TYPE(uint, unsigned int)
DX_CHECK_TYPE(byte, char)
DX_CHECK_TYPE(ubyte, unsigned char)
DX_CHECK_TYPE(short, short)
DX_CHECK_TYPE(ushort, unsigned short)
DX_CHECK_TYPE(ulong, unsigned long)
DX_CHECK_TYPE(int8, char)
DX_CHECK_TYPE(uint8, unsigned char)
DX_CHECK_TYPE(int16, short)
DX_CHECK_TYPE(uint16, unsigned short)
DX_CHECK_TYPE(int32, int)
DX_CHECK_TYPE(uint32, unsigned int)
DX_CHECK_TYPE(int64, long)
DX_CHECK_TYPE(uint64, unsigned long)
DX_CHECK_TYPE(float32, float)
DX_CHECK_TYPE(float64, double)

AC_C_BIGENDIAN

AC_LANG_CPLUSPLUS

AC_TRY_LINK(
[
#include <signal.h>
extern "C" void foo(int bar, ...){}
],
[
signal(SIGBUS, foo);
],
[
echo Using alternate signal argument list in C++
AC_DEFINE_UNQUOTED(ALTERNATE_CXX_SIGNAL)
],
[
echo Using standard signal argument list in C++
])

AC_LANG_C

DX_CHECK_SELECT_ARG
DX_CHECK_SOCK_LENGTH_TYPE

AC_OUTPUT(bin/dx bin/Makefile src/uipp/ui/Makefile lib/Makefile  \
	  src/exec/hwrender/gl/Makefile src/exec/hwrender/xgl/Makefile  \
	  src/exec/hwrender/starbase/Makefile src/uipp/dxuilib/Makefile \
	  src/uipp/ui/Makefile src/exec/Makefile \
	  src/exec/dxexec/Makefile include/Makefile \
	  include/dx/Makefile src/exec/libdx/Makefile \
	  src/Makefile src/exec/dpexec/Makefile \
	  src/exec/dxmods/Makefile src/uipp/base/Makefile \
	  src/uipp/Makefile src/uipp/dxl/Makefile \
	  src/uipp/dxui/Makefile src/uipp/prompter/Makefile \
	  src/uipp/mb/Makefile src/uipp/widgets/Makefile \
	  src/uipp/startup/Makefile src/uipp/tutor/Makefile \
	  src/uipp/java/dx/protocol/server/Makefile \
	  src/uipp/java/dx/protocol/Makefile src/uipp/java/dx/Makefile \
	  src/uipp/java/dx/runtime/Makefile \
	  src/uipp/java/dx/client/Makefile src/uipp/java/dx/net/Makefile \
	  src/uipp/java/dx/applets/Makefile src/uipp/java/dx/server/Makefile \
	  src/uipp/java/Makefile src/exec/hwrender/opengl/Makefile \
	  src/exec/hwrender/Makefile Makefile help/Makefile \
	  html/Makefile man/catl/Makefile man/manl/Makefile \
	  man/Makefile fonts/Makefile doc/Makefile \
	  src/misc/Makefile)
